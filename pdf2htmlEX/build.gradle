/*
 * pdf2htmlEX/build.gradle
 *
 * pdf2htmlEX-Android (https://github.com/ViliusSutkus89/pdf2htmlEX-Android)
 * Android port of pdf2htmlEX - Convert PDF to HTML without losing text or format.
 *
 * Copyright (c) 2019 - 2022 ViliusSutkus89.com
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 3 as
 * published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89'

//#UNCOMMENT-DURING-STAGING# /*
def gatherReleaseArtifactsTask = tasks.getByPath(':dependency-builder:gatherReleaseArtifacts')
def gatherDebugArtifactsTask = tasks.getByPath(':dependency-builder:gatherDebugArtifacts')
afterEvaluate {
    tasks.named('mergeReleaseJniLibFolders').get().dependsOn(gatherReleaseArtifactsTask)
    tasks.named('generateReleaseAssets').get().dependsOn(gatherReleaseArtifactsTask)
    tasks.named('mergeDebugJniLibFolders').get().dependsOn(gatherDebugArtifactsTask)
    tasks.named('generateDebugAssets').get().dependsOn(gatherDebugArtifactsTask)
}
//#UNCOMMENT-DURING-STAGING# */
android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 32

        versionCode 525
        versionName project.version

        archivesBaseName = rootProject.name

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        ndk.abiFilters = project.hasProperty('abi') ? [project.property('abi')] : ['arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64']
        externalNativeBuild.cmake.arguments '-DANDROID_STL=c++_shared'

        buildConfigField("String", "VERSION_NAME", "\"${rootProject.version}\"")
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    //#UNCOMMENT-DURING-STAGING# /*
    buildFeatures.prefab true
    externalNativeBuild.cmake {
        path 'src/main/cpp/CMakeLists.txt'
        version '3.22.1'
    }
    ndkVersion '23.2.8568313'
    buildTypes {
        release.externalNativeBuild.cmake.arguments '-DCMAKE_BUILD_TYPE=MinSizeRel', "-DINSTALLED_PREFIX=${gatherReleaseArtifactsTask.installed.get()}"
        debug.externalNativeBuild.cmake.arguments "-DINSTALLED_PREFIX=${gatherDebugArtifactsTask.installed.get()}"
    }
    sourceSets {
        release {
            jniLibs.srcDirs += gatherReleaseArtifactsTask.jniLibs.get()
            assets.srcDirs += gatherReleaseArtifactsTask.assets.get()
        }
        debug {
            jniLibs.srcDirs += gatherDebugArtifactsTask.jniLibs.get()
            assets.srcDirs += gatherDebugArtifactsTask.assets.get()
        }
    }
    //#UNCOMMENT-DURING-STAGING# */
}

dependencies {
    compileOnly project(":dependency-builder")

    implementation 'androidx.annotation:annotation:1.4.0'
    implementation 'com.getkeepsafe.relinker:relinker:1.4.5'
    implementation 'com.viliussutkus89:tmpfile-android:3.0.5'
    implementation 'com.viliussutkus89:fontconfig-android-translator:1.0.2'
    implementation 'com.viliussutkus89:ExecutableRunner:1.0.3'
    implementation 'com.viliussutkus89:assetextractor-android:1.3.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // License only dependencies. To add cpp dependencies to maven POM
    implementation 'com.viliussutkus89.licenseOnly:pdf2htmlEX-dependency-tree:0.18.18'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId rootProject.name
                version = rootProject.version

                pom {
                    name = rootProject.name
                    description = 'Android port of pdf2htmlEX - Convert PDF to HTML without losing text or format'
                    url = 'https://github.com/ViliusSutkus89/pdf2htmlEX-Android'
                    packaging = 'aar'
                    inceptionYear = '2019'
                    developers {
                        developer {
                            id = "ViliusSutkus89"
                            name = "Vilius Sutkus"
                            email = "ViliusSutkus89@gmail.com"
                        }
                    }
                    scm {
                        url = 'https://github.com/ViliusSutkus89/pdf2htmlEX-Android'
                        connection = 'https://github.com/ViliusSutkus89/pdf2htmlEX-Android.git'
                    }
                    licenses {
                        license {
                            name = 'GPLv3'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}
